/*
 * Copyright 2018 The BookLab Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "base"
    id "jacoco"
    id "com.github.ksoichiro.console.reporter" version "0.5.0"
}

allprojects {
    group = 'nl.tudelft.booklab'
    version = '1.0'

    ext {
        kotlin_version = '1.2.4`'
        ktor_version = '0.9.2'
        junit_jupiter_version = '5.1.1'
        junit_platform_version = '1.2.0'
    }
}

repositories {
    jcenter()
}

evaluationDependsOnChildren()

task jacocoTestReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        if (it.plugins.hasPlugin("java") || it.plugins.hasPlugin("kotlin")) {
            sourceSets it.sourceSets.main
            dependsOn it.test
        }
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        html.enabled = true
        html.destination file("${buildDir}/reports/jacoco/test/html")
        csv.enabled = false
    }
}

check.dependsOn jacocoTestReport

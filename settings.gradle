/*
 * Copyright 2018 The BookLab Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.internal.os.OperatingSystem

/* Workaround for Android plugin to support plugin syntax */
pluginManagement {
    resolutionStrategy {
        eachPlugin {
            if (requested.id.id == 'com.android.application') {
                useModule("com.android.tools.build:gradle:${requested.version}")
            }
        }
    }

    repositories {
        gradlePluginPortal()
        google()
    }
}

rootProject.name = "booklab"

/* Allow for modules to be excluded from a full build */
gradle.ext.exclude = hasProperty('exclude') ? getProperty('exclude').split(',')*.trim() : []
void tryInclude(String name) {
    if (!gradle.ext.exclude.contains(name)) {
        include name
    }
}

tryInclude 'booklab-backend'
tryInclude 'booklab-catalogue'
tryInclude 'booklab-catalogue-gbooks'
tryInclude 'booklab-catalogue-sru'
tryInclude 'booklab-deployment-testing'
tryInclude 'booklab-deployment-production'
tryInclude 'booklab-deployment-oss'

// Only include Android application if Android SDK is available (#111)
if (file('local.properties').exists() || System.env['ANDROID_HOME'] != null) {
	tryInclude 'booklab-frontend-android'
}

// Only include iOS application on macOS systems (#111)
if (OperatingSystem.current().isMacOsX()) {
	tryInclude 'booklab-frontend-ios'
}

tryInclude 'booklab-frontend-web'
tryInclude 'booklab-proxy'
tryInclude 'booklab-recommender'
tryInclude 'booklab-vision'
tryInclude 'booklab-vision-gvision'
tryInclude 'booklab-vision-tensorflow'
tryInclude 'booklab-vision-tesseract'
tryInclude 'ktor-auth-oauth2'


/*
 * Copyright 2018 The BookLab Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "java"
    id "application"
    id "jacoco"
    id "org.jetbrains.kotlin.jvm" version "1.2.41"
    id "org.jetbrains.kotlin.plugin.jpa" version "1.2.41"
    id "org.jetbrains.dokka" version "0.9.17"
    id "org.jmailen.kotlinter" version "1.12.0"
    id "com.bmuschko.docker-java-application" version "3.2.9"
}

/* Apply default Kotlin configuration */
apply from: '../gradle/kotlin.gradle'

/* Project configuration */
mainClassName = 'io.ktor.server.netty.DevelopmentEngine'

repositories {
    jcenter()
    maven { url = "https://dl.bintray.com/kotlin/ktor" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile "org.jetbrains.kotlin:kotlin-reflect"

    implementation project(":booklab-catalogue")
    implementation project(":booklab-vision")
    implementation project(":ktor-auth-oauth2")

    compile "io.ktor:ktor-server-core:$ktor_version"
    runtimeOnly "io.ktor:ktor-server-netty:$ktor_version"
    implementation "io.ktor:ktor-jackson:$ktor_version"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.+"

    compile "javax.persistence:javax.persistence-api:2.2"
    runtimeOnly "org.hibernate:hibernate-core:5.3.1.Final"
    runtimeOnly "com.h2database:h2:1.4.197"

    runtimeOnly "org.slf4j:slf4j-jdk14:1.7.25"

    testImplementation("io.ktor:ktor-server-test-host:$ktor_version") {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0-alpha04"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    testImplementation "org.junit.jupiter:junit-jupiter-params:$junit_jupiter_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
    testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junit_platform_version"
}

/* Docker build setup */
dockerDistTar {
    volume "/config"
    environmentVariable('GOOGLE_APPLICATION_CREDENTIALS', '/config/keys.json')
}

docker {
    if (System.env["GITLAB_CI"] != null) {
        registryCredentials {
            url = System.env["CI_REGISTRY"]
            username = System.env["CI_REGISTRY_USER"]
            password = System.env["CI_REGISTRY_PASSWORD"]
        }
    }

    javaApplication {
        maintainer = 'Fabian Mastenbroek "f.s.mastenbroek@student.tudelft.nl"'
        tag = "booklab-backend:${project.version}"
    }
}

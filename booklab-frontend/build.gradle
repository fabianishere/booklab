/*
 * Copyright 2018 The BookLab Authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "base"
    id "com.bmuschko.docker-remote-api" version "3.2.9"
}

import com.bmuschko.gradle.docker.DockerRemoteApiPlugin
import com.bmuschko.gradle.docker.tasks.image.Dockerfile
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

task createDockerfile(type: Dockerfile) {
    group = DockerRemoteApiPlugin.DEFAULT_TASK_GROUP
    description = 'Create Dockerfile for project'

    from 'node:8 as node'
    maintainer 'Fabian Mastenbroek "f.s.mastenbroek@student.tudelft.nl"'
    workingDir '/app'
    copyFile 'package.json', '/app/'
    runCommand 'npm install'
    copyFile './',  '/app/'
    environmentVariable 'env', 'prod'
    runCommand 'npm run build'

    from 'nginx'
    instruction 'COPY --from=node /app/build/angular/ /usr/share/nginx/html'
    copyFile './nginx.conf', '/etc/nginx/conf.d/default.conf'
}

task dockerSyncResources(type: Sync) {
    group = DockerRemoteApiPlugin.DEFAULT_TASK_GROUP
    description = 'Copies the distribution resources to a temporary directory for image creation.'
    from { project.projectDir }
    into { createDockerfile.destFile.parentFile }
    exclude 'node_modules'
    exclude 'build'
}

createDockerfile.dependsOn dockerSyncResources

task dockerBuildImage(type: DockerBuildImage) {
    dependsOn createDockerfile
    inputDir = createDockerfile.destFile.parentFile
    inputs.files fileTree(project.projectDir).filter {
        exclude 'build'
    }

    tag = "booklab-frontend:${project.version}"
}
